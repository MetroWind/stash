pkgname=stash
pkgver=0.2.0
pkgrel=1
pkgdesc='A naively simple read-it-later service'
arch=(x86_64 i686 armv6h armv7h)
url='https://github.com/MetroWind/stash'
license=(WTFPL)
makedepends=(sqlite rustup git)
depends=(sqlite)
source=("git+${url}.git" "sysusers-stash.conf" "stash.service")
sha256sums=('SKIP'
            '975c2e5d16c8a91335845fd1223359d704c2b7007de6c301c09c85c4b6e0013b'
            'c7ba4e06a93dbc0bb52c54a7d2112f03f751a5e70247abd9580d25463e5929a8')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    rustup install --profile minimal stable
}

build() {
    cd "${srcdir}/${pkgname}"
    export CARGO_TARGET_DIR=target
    cargo build --release
}

package() {
    install -Dm0755 -t "$pkgdir/usr/bin/" "${srcdir}/${pkgname}/target/release/${pkgname}"
    mkdir -pv "$pkgdir/var/lib/${pkgname}"
    cp -r "${srcdir}/${pkgname}/templates" "$pkgdir/var/lib/${pkgname}/"
    cp -r "${srcdir}/${pkgname}/static" "$pkgdir/var/lib/${pkgname}/"
    install -Dm644 "${srcdir}/${pkgname}/stash-example.toml" "${pkgdir}/etc/stash.toml"
    install -Dm644 "sysusers-stash.conf" "${pkgdir}/usr/lib/sysusers.d/stash.conf"
    install -Dm644 "stash.service" "${pkgdir}/usr/lib/systemd/system/stash.service"
}
